CATEGORY Geometry

PARAM Tex         STRING
PARAM Tex2        STRING  DEFAULT Tex
PARAM Solid       BOOLEAN DEFAULT True
PARAM Transparent BOOLEAN DEFAULT False
PARAM SemiTrans   BOOLEAN DEFAULT False
PARAM Masked      BOOLEAN DEFAULT False
PARAM FullTexture BOOLEAN DEFAULT False
PARAM TexScale    FLOAT   DEFAULT 1
PARAM Stretch     BOOLEAN DEFAULT False
PARAM TopScale    FLOAT   DEFAULT 1
PARAM XShift      FLOAT   DEFAULT 0
PARAM Back        BOOLEAN DEFAULT True

HINT  Tex         The front texture.
HINT  Tex2        The back texture. Defaults to Tex.
HINT  Solid       When True, makes sure that the player can't pass through the object.
HINT  Transparent When True, makes the object fully transparent.
HINT  SemiTrans   When True, makes the object 50% transparent.
HINT  Masked      When True, means that the textures used are 8-bit masked textures
HINT  Masked      (e.g. tree leaves) where the first color in the color palette is
HINT  Masked      the transparent color.
HINT  FullTexture When True, allows TexScale and Stretch to determine how the
HINT  FullTexture texture is mapped onto the faces. Otherwise, OpenZone uses its
HINT  FullTexture internal algorithm based on overall object size to map textures
HINT  FullTexture onto faces.
HINT  TexScale    Acts as a multiplier for horizontal and vertical texture coordinates
HINT  TexScale    when FullTexture is True.
HINT  Stretch     When True, stretches the texture horizontally based on X size
HINT  Stretch     vs. height.
HINT  TopScale    Acts as a multiplier for the width of the top of the sheet
HINT  TopScale    relative to the bottom. This can be used to taper (or expand)
HINT  TopScale    the sheet as it rises.
HINT  XShift      Shifts the top of the sheet relative to the bottom in the X
HINT  XShift      direction. This is specified in fractional units; a value
HINT  XShift      of 1 would shift by the object's X size, for instance.
HINT  XShift      Negative values can be used to shift in the opposite direction.

VARIABLE X  FLOAT
VARIABLE Z  FLOAT
VARIABLE X1 FLOAT
VARIABLE X2 FLOAT
VARIABLE S  FLOAT

S = 0.5 - (TopScale / 2)

If FullTexture
  X = TexScale
  If SizeX <> 0
    Z = (SizeZ / SizeX) * TexScale
  Else
    Z = TexScale
  EndIf
  If Stretch
    Z = X
  EndIf
  X1 = (S-XShift)   * TexScale
  X2 = (1-S-XShift) * TexScale

  RECTANGLETEX 0,0,0, S-XShift,0,1,   1-S-XShift,0,1, 1,0,0, 0,0, X1,Z, X2,Z, X,0, Tex,  Transparent, SemiTrans, Solid, 0, false, Masked   ' Front face
  If Back
    RECTANGLETEX 1,0,0, 1-S-XShift,0,1, S-XShift,0,1,   0,0,0, X,0, X2,Z, X1,Z, 0,0, Tex2, Transparent, SemiTrans, Solid, 0, false, Masked   ' Back face
  EndIf
Else
  RECTANGLE 0,0,0, S-XShift,0,1,   1-S-XShift,0,1, 1,0,0, Tex,  Transparent, SemiTrans, Solid, 0, false, Masked   ' Front face
  If Back
    RECTANGLE 1,0,0, 1-S-XShift,0,1, S-XShift,0,1,   0,0,0, Tex2, Transparent, SemiTrans, Solid, 0, false, Masked   ' Back face
  EndIf
EndIf
